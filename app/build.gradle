apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "fi.ketjusovellus.ketjupilotti"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 5
        versionName "0.1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('YOUR_KEYSTORE_HERE.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias = 'YOUR_SIGNING_KEY'
            keyPassword KEY_PASSWORD
        }
        qa {
            storeFile file('YOUR_KEYSTORE_HERE.jks')
            storePassword KEYSTORE_PASSWORD
            keyAlias = 'YOUR_SIGNING_KEY'
            keyPassword KEY_PASSWORD
        }
    }

    flavorDimensions "version"
    productFlavors {
        calibration {
            dimension "version"
        }
        production {
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "KETJU_BACKEND_URL", KETJU_BACKEND_URL_PROD
            buildConfigField "String", "KETJU_BACKEND_JWT_PUBLIC_KEY", KETJU_BACKEND_JWT_PUBLIC_KEY_PROD
            buildConfigField "String", "KETJU_BACKEND_CERTIFICATE_HASH", KETJU_BACKEND_CERTIFICATE_HASH_PROD
        }
        qa {
            initWith(release)
            applicationIdSuffix ".qa"
            signingConfig signingConfigs.qa
            matchingFallbacks = ['release']

            buildConfigField "String", "KETJU_BACKEND_URL", KETJU_BACKEND_URL_QA
            buildConfigField "String", "KETJU_BACKEND_JWT_PUBLIC_KEY", KETJU_BACKEND_JWT_PUBLIC_KEY_QA
            buildConfigField "String", "KETJU_BACKEND_CERTIFICATE_HASH", KETJU_BACKEND_CERTIFICATE_HASH_QA
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"

            buildConfigField "String", "KETJU_BACKEND_URL", KETJU_BACKEND_URL_DEV
            buildConfigField "String", "KETJU_BACKEND_JWT_PUBLIC_KEY", KETJU_BACKEND_JWT_PUBLIC_KEY_DEV
            buildConfigField "String", "KETJU_BACKEND_CERTIFICATE_HASH", KETJU_BACKEND_CERTIFICATE_HASH_DEV
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.fragment:fragment-ktx:1.2.4"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "com.airbnb.android:lottie:3.4.0"
    implementation "com.squareup.okhttp3:okhttp:4.7.2"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    // or Koin for Lifecycle scoping
    implementation "org.koin:koin-android-scope:$koin_version"
    // or Koin for Android Architecture ViewModel
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // DP-3T custom fork:
    implementation project(':dp3t-sdk')

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

}
